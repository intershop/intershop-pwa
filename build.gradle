plugins {
    /** https://github.com/srs/gradle-node-plugin */
    id "com.moowork.node" version "1.1.1"
}

/** Gretty Plugin for running jetty
      http://akhikhl.github.io/gretty-doc/Getting-started.html 
  */
apply plugin: 'war'
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'

node {
    version = '6.10.3'
    npmVersion = '3.10.10'
    download = false
    // distBaseUrl = Custom artifactory location here for node/npm.
}

task npmClean(type:Delete) {
    delete 'dist'

    /** uncomment to delete node environment with clean */
//    delete 'node_modules', '.gradle/npm', '.gradle/nodejs'
}
clean.dependsOn npmClean

task npmBuild(type: NpmTask, dependsOn:npmInstall, description: 'run npm distribution') {
    args = ['run', 'build']
}

task copyNodeModulesToDist(type:Copy, dependsOn:npmBuild) {
    from 'node_modules'
    into 'dist/node_modules'
    // TODO: FILTER!!! or solve by projecting 
}
gradle.projectsEvaluated {
    [war]*.dependsOn copyNodeModulesToDist
    tasks.find { 
        it.class.name.startsWith('org.akhikhl.gretty.') && it.class.name.contains('Start') 
    }*.dependsOn copyNodeModulesToDist    
}    

war { 
    from('dist') /** include npm generated output into war */
    webXml = file('src/web.xml')
} 
war.dependsOn npmBuild

gretty {
    extraResourceBase 'dist' /** use npm generated output */
    
    /** same as angular cli */
    httpPort = 4200 
    contextPath = '/'
}
